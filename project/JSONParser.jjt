options {
    MULTI = true;
    VISITOR = true;
}

PARSER_BEGIN(JSONParser)
import java.io.*;

class JSONParser
{
    public static void main(String[] args ) throws ParseException,Exception
    {
        try
        {
            JSONParser analizador = new JSONParser(System.in);
            analizador.Programa().jjtAccept(new JSONToYAMLVisitor(), 0);
            //SimpleNode n = analizador.Programa();
            //n.dump(">");
        }
        catch(ParseException e)
        {
            System.out.println(e.getMessage());
            System.out.println("Error al parsear");
        }

    }
}

class JSONToYAMLVisitor implements JSONParserVisitor {

    public Object visit(SimpleNode node, Object data) {
        return null;
    }
    public Object visit(ASTRoot node, Object data) {
        node.childrenAccept(this, data);
        return null;
    }
    public Object visit(ASTObject node, Object data) {
        int nivelAnidacion = (Integer) data + 1;
        if (nivelAnidacion > 1) System.out.print("\n");
        node.childrenAccept(this, nivelAnidacion);
        return null;
    }
    public Object visit(ASTListaIdentificadorValor node, Object data) {
        int nivelAnidacion = (Integer) data;
        String prefijo = "";
        for(int i = 1; i < nivelAnidacion; i++) prefijo = prefijo + "    ";
        System.out.print(prefijo);
        node.childrenAccept(this, data);
        return null;
    }
    public Object visit(ASTIdentificadorValor node, Object data) {
        node.childrenAccept(this, data);
        return null;
    }

    public Object visit(ASTIdentificador node, Object data) {
		String label = (String) node.jjtGetValue();
        System.out.print(label+": ");
        node.childrenAccept(this, data);
        return null;
    }

    public Object visit(ASTArray node, Object data) {
        int nivelAnidacion = (Integer) data + 1;
        if (nivelAnidacion > 1) System.out.print("\n");
        node.childrenAccept(this, nivelAnidacion);
        return null;
    }
    public Object visit(ASTListaValor node, Object data) {
        int nivelAnidacion = (Integer) data;
        String prefijo = "";
        for(int i = 1; i < nivelAnidacion; i++) prefijo = prefijo + "    ";
        System.out.print(prefijo + "- ");
        node.childrenAccept(this, data);
        return null;
    }
    public Object visit(ASTValor node, Object data) {
        node.childrenAccept(this, data);
        return null;
    }
    public Object visit(ASTValorPrimitivo node, Object data) {
		String label = (String) node.jjtGetValue();
        System.out.print(label+"\n");
        node.childrenAccept(this, data);
        return null;
    }
}

PARSER_END(JSONParser)

// Analisis lexico

// TOKEN DE SIMBOLOS QUE SE USAN EN LA SINTAXIS
TOKEN :
{
  <ASIGNACION : "=">  // 1
  | <LBRACE:"{">      // 2
  | <RBRACE:"}">      // 3
  | <LBRACKET:"[">    // 4
  | <RBRACKET:"]">    // 5
  | <COMMA:",">       // 6
  | <DOTDOT:":">      // 7

}


// TOKEN DE VALORES RESERVADOS "PRIMITIVOS"
TOKEN :
{
  <TRUE:"true">       // 8
  |<FALSE:"false">    // 9
  |<NULL:"null">      // 10
}

// TOKEN DE SIMBOLOS QUE DENOTAN VALORES EN EL MUNDO JSON
TOKEN :
{
  <IDENTIFIER:["a"-"z","A"-"Z"](["a"-"z","A"-"Z","0"-"9","_"])*>      // 11
  | <NUMBER: (["0"-"9"])+>                                            // 12
  | <DECIMAL: (["0"-"9"])+["."](["0"-"9"])+>                          // 13
  | <EXPONENCIAL: (["0"-"9"])+["."](["0"-"9"])+["e"](["0"-"9"])+>     // 14
}

// CARACTERES QUE NO SE TOMAN EN CUENTA
SKIP :
{
  " "|"\n"|"\r"|"\r\n"|"\t"
}

// Analisis Sintactico

// ALGO en JSON es :
// *  un objeto {clave:valor,clave2:valor2,.......}
// *  un array   [valor1,valor2,valor3,....]
ASTRoot Programa() #Root :
{}
{
(Object()|Array())
{ return jjtThis; }
}

void Object() #Object :
{}
{
<LBRACE> ListaIdentificadorValor() <RBRACE>
}

void ListaIdentificadorValor() #ListaIdentificadorValor :
{}
{
IdentificadorValor() (LOOKAHEAD(2) <COMMA> ListaIdentificadorValor())*
}

void IdentificadorValor() #IdentificadorValor :
{}
{
(Identificador() <DOTDOT> Valor())
}

void Identificador() #Identificador :
{}
{
<IDENTIFIER>
{ jjtThis.jjtSetValue(token.image); }
}

void Array() #Array :
{}
{
<LBRACKET> ListaValor() <RBRACKET>
}

void ListaValor() #ListaValor :
{}
{
Valor() (LOOKAHEAD(2) <COMMA> ListaValor())*
}

void Valor() #Valor :
{}
{
(ValorPrimitivo()|Array()|Object())
}

void ValorPrimitivo() #ValorPrimitivo :
{}
{
(<IDENTIFIER>|<NUMBER>|<DECIMAL>|<EXPONENCIAL>|<FALSE>|<TRUE>|<NULL>)
{ jjtThis.jjtSetValue(token.image); }
}
